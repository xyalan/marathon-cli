plugins {
    id 'net.researchgate.release' version '2.4.0'
}
group 'com.github.xyalan'
version '0.4.4-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

release {
    tagTemplate = '${version}.RELEASE'
}

repositories {
    mavenCentral()
}

ext {
    feign_version = '8.17.0'
    slf4j_version = '1.7.21'
}

dependencies {
    compile(
            "com.netflix.feign:feign-core:$feign_version",
            "com.netflix.feign:feign-gson:$feign_version",
            "org.slf4j:slf4j-api:$slf4j_version"
    )
}


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'marathon-cli'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A application used as an example on how to set up pushing  its components to the Central Repository.'
                url 'http://www.example.com/example-application'

                scm {
                    connection 'scm:git:git://github.com/xyalan/marathon-cli.git'
                    developerConnection 'scm:git:git@github.com:xyalan/marathon-cli.git'
                    url 'https://github.com/xyalan/marathon-cli'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'alan'
                        name 'Alan Yang'
                        email 'alanlhy@gmail.com'
                    }
                }
            }
        }
    }
}